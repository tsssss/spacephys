;+
; Load data of r_gsm, b_gsm. Calculate dis, MLT, MLat, the detrended tilt angle.
; Downsample to the common data rate.
;-

pro azim_df_load_data, reload=reload, project=project, event_id=event_ids

    if n_elements(project) eq 0 then project = azim_df_load_project()

;---Make sure we have done storm finding.
    if ~project.haskey('done_find_storm') then azim_df_find_storm, project=project
    storm_ids = project.storm_id

;---Prepare for event_ids.
    nevent_id = n_elements(event_ids)
    if nevent_id eq 0 then begin
        event_ids = storm_ids
    endif else begin
        flags = bytarr(nevent_id)
        foreach event_id, event_ids, ii do begin
            index = where(storm_ids eq event_id, count)
            flags[ii] = count gt 0
        endforeach
        index = where(flags ne 0, count)
        if count eq 0 then begin
            errmsg = handle_error('No valid event_id ...')
            return
        endif else event_ids = event_ids[index]
    endelse

;---Load data for each event_id.
    pad_time = project['storm_setting'].pad_time
    storm_times = project.storm_times
    mission_probes = project.mission_probes
    time_step = project.time_step
    foreach event_id, event_ids do begin
        index = where(storm_ids eq event_id)
        time_range = storm_times[index[0]]
        time_range = time_range+[-1,1]*pad_time
        file = join_path([project.data_dir,event_id+'_data.tplot'])
        azim_df_load_data_per_event, time_range=time_range, reload=reload, mission_probes=mission_probes, time_step=time_step, filename=file
    endforeach


end
