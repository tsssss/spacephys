;+
; Read theta.
;-

pro azim_dp_read_theta_gen_file, time, probe=probe, filename=file

;---Check inputs.
    if n_elements(file) eq 0 then begin
        errmsg = handle_error('No output file ...')
        return
    endif

    if n_elements(probe) eq 0 then begin
        errmsg = handle_error('No input probe ...')
        return
    endif

    if n_elements(time) eq 0 then begin
        errmsg = handle_error('No input time ...')
        return
    endif


;---Constants and settings.
    secofday = constant('secofday')
    errmsg = ''
    xyz = constant('xyz')

    ; Derived settings.
    date = time[0]
    date = date-(date mod secofday)
    time_range = date+[0,secofday]
    prefix = probe+'_'
    probe_info = resolve_probe(probe)


;---Init file.
    out_dir = fgetpath(file)
    if file_test(out_dir,/directory) eq 0 then file_mkdir, out_dir
    data_file = file
    if file_test(data_file) eq 0 then begin
        ginfo = dictionary($
            'TITLE', 'theta, the detrended tilt angle, for dipolarization study', $
            'TEXT', 'Generated by Sheng Tian at the University of Minnesota' )
        cdf_id = cdf_create(data_file)
        cdf_save_setting, ginfo, filename=cdf_id
    endif else cdf_id = cdf_open(data_file)


;---Common time.
    bfield_time_var = 'bfield_ut'
    if ~cdf_has_var(bfield_time_var, filename=cdf_id) then begin
        bfield_time_step = 10.
        bfield_times = make_bins(time_range+[0,-1]*bfield_time_step, bfield_time_step)
        cdf_save_var, bfield_time_var, value=bfield_times, filename=cdf_id
        setting = dictionary($
            'description', 'ut sec of bfield data', $
            'unit', 'sec', $
            'time_step', bfield_time_step )
        cdf_save_setting, setting, varname=bfield_time_var, filename=cdf_id
    endif

    dummy_time_var = 'dummy_ut'
    if ~cdf_has_var(dummy_time_var, filename=cdf_id) then begin
        dummy_time_step = 10.
        cdf_save_var, dummy_time_var, value=time_range+[0,-1]*dummy_time_step, filename=cdf_id
        setting = dictionary($
            'description', 'ut sec of the time range', $
            'unit', 'sec' )
        cdf_save_setting, setting, varname=dummy_time_var, filename=cdf_id
    endif

    smooth_window = constant('secofhour')
    azim_dp_read_level2_data, time_range+[-1,1]*smooth_window, probe=probe, errmsg=errmsg, datatype='dtilt'


    theta_var = prefix+'theta'
    get_data, prefix+'dtilt', times, dtilt
    index = where(finite(dtilt), count)
    have_data = (count ge 2)? 1: 0

    if have_data then begin
        time_step = sdatarate(times)
        smooth_width = smooth_window/time_step
        theta = dtilt-smooth(dtilt, smooth_width, /edge_truncate, /nan)
        store_data, theta_var, times, theta
        common_times = cdf_read_var(bfield_time_var, filename=cdf_id)
        interp_time, theta_var, common_times
        value = get_var_data(theta_var)
        time_var = bfield_time_var
    endif else begin
        value = fltarr(2)+!values.f_nan
        time_var = dummy_time_var
    endelse

    setting = dictionary($
        'have_data', have_data, $
        'display_type', 'scalar', $
        'short_name', tex2str('theta'), $
        'unit', 'deg', $
        'depend_0', time_var )

    cdf_save_var, theta_var, value=float(value), filename=cdf_id
    cdf_save_setting, setting, varname=theta_var, filename=cdf_id
    
    cdf_close, cdf_id
end


time_range = time_double(['2019-08-01','2019-08-10'])
probes = ['rbspa','rbspb','mms1','g15','g16','g17','tha','thd','the']

time_range = time_double(['2019-09-06','2019-09-07'])
probes = ['g15']
file = join_path([homedir(),'test.cdf'])
file_delete, file
;time_range = time_double(['2014-08-28','2014-08-29'])
;probes = ['rbspb','g13','g15','tha','thd','the']

foreach probe, probes do begin
    azim_dp_read_theta_gen_file, time_range, probe=probe, filename=file
endforeach
end